/*
    First order logic parser, for J-Golo specifications.
    Initialy inspired by Fol parser :
        https://github.com/rindPHI/FirstOrderParser
    Official WhyML Parser :
        https://github.com/jpdeplaix/why3/blob/master/src/parser/lexer.mll
        https://github.com/jpdeplaix/why3/blob/master/src/parser/parser.mly
*/

// =====> How to correctly cite the source (original Copyright below)
/* Copyright 2014 Dominic Scheurer
 *
 * This file is part of FirstOrderParser.
 *
 * FirstOrderParser is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * FirstOrderParser is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with FirstOrderParser.  If not, see <http://www.gnu.org/licenses/>.
 */

options
{
	LOOKAHEAD = 1;
	JDK_VERSION = "1.6";
	static = false;
}

PARSER_BEGIN(FolJGoloParser)
package org.eclipse.golo.compiler.jgoloparser;

import java.io.StringReader;
import java.util.ArrayList;

//import org.eclipse.golo.compiler.jgoloparser.*;

/**
 * <p>
 * Parser for formulas of the language of first-order (predicate)
 * logic. The suggested method to start with is {@link #parse(String)}.
 * </p>
 *
 * <p>
 * Original Syntax: Variables start with upper letters, functions and predicates
 * with lower letters. Digits and underscores in identifiers are allowed.
 * The binary operators "&", "|", "->" must be put in parentheses; parentheses
 * in other places are not allowed. The negation operator is "!", the
 * universal quantifier is "forall", the existential quantifier is "exists".
 * Whitespace is ignored.
 * </p>
 *
 * <p>
 *  WhyML changes :
 *   + & -> /\
 *   + | -> \/
 * </p>
 *
 * <p>
 * <pre>
 * term  ::= var | fun | fun terms
 * terms ::= "(" term ")" | "(" term "," terms ")"
 * form  ::=   pred
 *           | pred terms
 *           | "!" form
 *           | "(" form "/\" form ")"
 *           | "(" form "\/" form ")"
 *           | "(" form "->" form ")"
 *           | "exists " var "." form
 *           | "forall " var "." form
 *
 * spec ::=   ensures "{" form "}"
 *          | requires "{" form "}"
 *          | invariant "{" form "}"
 *          | variant "{" form "}"
 *
 * specs ::= spec | spec specs
 * </pre>
 * </p>

 TODO : no distinction between variables and fun through first lettter capitlisation
 TODO : add spec and specs
 TODO : add arithmetics
 TODO : ...
 *
 * @author Dominic Scheurer
 * @author Nicolas Stouls
 */
public class FolJGoloParser
{
    /**
     * Parses a given formula.
     *
     * @param formula JGFormula to parse.
     * @return Parsed formula object.
     * @throws ParseException if an error occurs during parsing.
     */
    public static JGSpecs parse(String formula) throws ParseException
    {
        FolJGoloParser parser =
        		new FolJGoloParser(new StringReader(formula));
        return parser.parse();
    }
}
PARSER_END(FolJGoloParser)

SKIP :
{
 	" "
|	"\t"
|	"\r"
|	"\n"
|	"\f"
}

TOKEN :
{
	< OPEN: "(" >
|	< CLOSE: ")" >
|	< COMMA: "," >
|	< DOT: "." >

|	< NEG: "!" >
|	< CONJ: "/\\" >
|	< DISJ: "\\/" >
|	< IMP: "->" >
|	< ALL: "forall" >
|	< EXIST: "exists" >

| < ENSURES: "ensures" >
| < REQUIRES: "requires" >
| < INVARIANT: "invariant" >
| < VARIANT: "variant" >


| < VARIABLE: < UPPER > (< INNER >)* >
| < ID: < LOWER > (< INNER >)* >

|   < #INNER: (<UPPER> | <UNDER> | <LOWER> | <DIGIT>) >
|   < #DIGIT: ["0" - "9"] >
|   < #LOWER: ["a" - "z"] >
|   < #UPPER: ["A" - "Z"] >
|   < #UNDER: "_"  >

}

JGSpecs parse() :
{
    JGSpecs specs;
}
{
    specs = specs()
    < EOF >
    {
        return specs;
    }
}

JGSpecs specs() :
{
    JGSpecs specs = new JGSpecs();
    JGSpec spec;
}
{
    (
        spec = spec()
        {
          specs.add(spec);
        }
    )+
    {
        return specs;
    }
}


JGSpec spec() :
{
    JGFormula form;
    String specType;
}
{

    specType=specType()
    "{"
    form=formula()
    "}"
    {
        return new JGSpec(specType,form);
    }
}


String specType() :
{
    String specType;
}
{

    < ENSURES >   {return "ensures"; } |
    < REQUIRES >  {return "requires"; } |
    < INVARIANT > {return "invariant"; } |
    < VARIANT >   {return "variant"; }
}



JGFormula formula() :
{
    JGFormula form;
}
{
	(
	    form = atomic()      |
	    form = negated()     |
	    form = existential() |
	    form = universal()   |
	    form = binary()
	)
	{
		return form;
	}
}

JGExistential existential() :
{
    JGFormula inner;
    JGTerm qfdVar;
}
{
	< EXIST >
	< VARIABLE >
	{
	    qfdVar = new JGTerm(token.image);
	}
	< DOT >
	inner = formula()
	{
	    return new JGExistential(inner, qfdVar);
	}
}

JGUniversal universal() :
{
    JGFormula inner;
    JGTerm qfdVar;
}
{
	< ALL >
	< VARIABLE >
	{
	    qfdVar = new JGTerm(token.image);
	}
	< DOT >
	inner = formula()
	{
	    return new JGUniversal(inner, qfdVar);
	}
}

JGNegated negated() :
{
    JGFormula inner;
}
{
	< NEG >
	inner = formula()
	{
	    return new JGNegated(inner);
	}
}

JGFormula binary() :
{
    JGFormula innerA, innerB;
}
{
    < OPEN >
	innerA = formula()
	(
	    < IMP >
		innerB = formula()
		< CLOSE >
		{
		    return new JGImplicative(innerA, innerB);
		}
    |
	    < DISJ >
		innerB = formula()
		< CLOSE >
		{
		    return new JGDisjunctive(innerA, innerB);
		}
	|
	    < CONJ >
		innerB = formula()
		< CLOSE >
		{
		    return new JGConjunctive(innerA, innerB);
		}
	)
}

JGAtomic atomic() :
{
    JGPredicate p;
    ArrayList<JGTerm> arguments;
}
{
	< ID >
	{
	    p = new JGPredicate(token.image);
	}
	(
		arguments = tuple()
		{
		    return new JGAtomic(p, arguments);
		}
    |
		{
		    return new JGAtomic(p);
		}
	)
}

JGTerm term() :
{
    String function;
    ArrayList<JGTerm> arguments = new ArrayList<JGTerm>();
}
{
    < VARIABLE >
    {
        return new JGTerm(token.image);
    }
  | < ID >
    {
    	function = token.image;
    }
    (
		arguments = tuple()
    )?
    {
    	return new JGTerm(function, arguments);
	}
}

ArrayList<JGTerm> tuple() :
{
    ArrayList<JGTerm> tuple = new ArrayList<JGTerm>();
    JGTerm t;
}
{
    < OPEN >
    t = term()
    {
        tuple.add(t);
    }
    (
        < COMMA > t = term()
        {
            tuple.add(t);
        }
    )*
    < CLOSE >
    {
		return tuple;
    }
}
